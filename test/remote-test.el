(ert-deftest remote-urls ()
  (with-temp-dir (path remote-a remote-b)
    (in-dir remote-a
      (init)
      (commit-change "test-a" "content"))
    (in-dir remote-b
      (init)
      (commit-change "test-b" "content"))
    (in-dir path
      (init)
      (run "git" "remote" "add" "a" (concat "file://" remote-a))
      (run "git" "remote" "add" "b" (concat "file://" remote-b))
      (run "git" "remote" "set-url" "--push" "b" "some-url")
      (let* ((repo (libgit-repository-open path))
             (ra (libgit-remote-lookup repo "a"))
             (rb (libgit-remote-lookup repo "b")))
        (should (equal '("a" "b") (libgit-remote-list repo)))
        (should (libgit-remote-p (libgit-remote-lookup repo "a")))
        (should-error (libgit-remote-lookup repo "c") :type 'giterr)
        (should (string-prefix-p "file://" (libgit-remote-url ra)))
        (should (path= remote-a (substring (libgit-remote-url ra) 7)))
        (should-not (libgit-remote-pushurl ra))
        (should (string-prefix-p "file://" (libgit-remote-url rb)))
        (should (path= remote-b (substring (libgit-remote-url rb) 7)))
        (should (string= "some-url" (libgit-remote-pushurl rb)))))))

(ert-deftest remote-refspecs ()
  (with-temp-dir (path rpath-a rpath-b)
    (in-dir rpath-a
      (init)
      (commit-change "test-a" "content"))
    (in-dir rpath-b
      (init)
      (commit-change "test-b" "content")
      (run "git" "checkout" "-b" "branchname"))
    (in-dir path
      (init)
      (run "git" "remote" "add" "rema" (concat "file://" rpath-a))
      (run "git" "remote" "add" "-t" "branchname" "-t" "otherbranch" "remb" (concat "file://" rpath-a))
      (let* ((repo (libgit-repository-open path))
             (remote-a (libgit-remote-lookup repo "rema"))
             (remote-b (libgit-remote-lookup repo "remb")))
        (should (= 1 (libgit-remote-refspec-count remote-a)))
        (should (= 2 (libgit-remote-refspec-count remote-b)))
        (should (equal '("+refs/heads/*:refs/remotes/rema/*")
                       (libgit-remote-get-refspecs remote-a 'fetch)))
        (should (equal '("+refs/heads/branchname:refs/remotes/remb/branchname"
                         "+refs/heads/otherbranch:refs/remotes/remb/otherbranch")
                       (libgit-remote-get-refspecs remote-b 'fetch)))
        (should-not (libgit-remote-get-refspecs remote-a 'push))
        (should-not (libgit-remote-get-refspecs remote-b 'push))
        (let ((spec (libgit-remote-get-refspec remote-b 0)))
          (should (eq 'fetch (libgit-refspec-direction spec)))
          (should (string= "refs/remotes/remb/branchname" (libgit-refspec-dst spec)))
          (should (string= "refs/heads/branchname" (libgit-refspec-src spec)))
          (should (string= "+refs/heads/branchname:refs/remotes/remb/branchname" (libgit-refspec-string spec)))
          (should (libgit-refspec-force-p spec)))))))
